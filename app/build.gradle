repositories {

    jcenter()

}

apply plugin: 'com.android.application'

ext {

    allFlavors = [
            "wcr"    : [packageName    : "fr.pasteque.client.wcr",
                        applicationName: "wcr"],

            "bnp"    : [packageName: "fr.pasteque.client.bnp"],

            "vanilla": [packageName    : "fr.pasteque.client.vanilla",
                        applicationName: "Past√®que"]
    ]
}

//Needs to be placed after ext{account=...}
if (new File(project(":app").getProjectDir().getPath() + "/account.gradle").exists()) {
    apply from: "account.gradle"
}


task flavors << {

    allFlavors.each { name, config ->
        println "$name"
    }

}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    signingConfigs {
        release {
            def propsFile = project(":app").file("keystore.properties")
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {

        allFlavors.each { name, config ->
            "$name" {
                applicationId config.packageName
                if (config.containsKey('applicationName')) {
                    resValue "string", "app_name", config.applicationName
                }

                if (project.ext.has("account")) {
                    resValue "string", "default_user", account.user
                    resValue "string", "default_password", account.password
                    resValue "string", "default_cash", account.cash
                    resValue "bool", "hasDefaultAccount", "true"
                } else {
                    resValue "string", "default_user", ""
                    resValue "string", "default_password", ""
                    resValue "string", "default_cash", ""
                    resValue "bool", "hasDefaultAccount", "false"

                }
            }

        }

    }

}

dependencies {

    androidTestCompile 'com.android.support.test:runner:0.3'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.powermock:powermock-module-junit4:1.6.2'
    androidTestCompile 'org.powermock:powermock-api-easymock:1.6.2'
    androidTestCompile 'org.easymock:easymock:3.3.1'
    androidTestCompile 'org.skyscreamer:jsonassert:1.2.3'

    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.zxing:core:3.2.0'
    compile fileTree(dir: 'libs', include: '*.jar')

}
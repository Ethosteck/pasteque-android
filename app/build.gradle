repositories {

    jcenter()

}

apply plugin: 'com.android.application'

ext {
    flavors = [
            "wcr"    : [packageName    : "fr.pasteque.client.wcr",
                        applicationName: "wcr"],

            "bnp"    : [packageName: "fr.pasteque.client.bnp"],

            "vanilla": [packageName    : "fr.pasteque.client.vanilla",
                        applicationName: "Past√®que"]
    ]
}

task flavors << {
    flavors.each { name, config ->
        println "$name"
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {

        flavors.each { name, config ->
            "$name" {
                applicationId config.packageName
                if (config.containsKey('applicationName')) {
                    resValue "string", "app_name", config.applicationName
                }
            }

        }

    }

}

dependencies {

    androidTestCompile 'com.android.support.test:runner:0.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.28-beta'
    testCompile 'org.skyscreamer:jsonassert:1.2.3'

    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.android.support:support-v13:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.zxing:core:3.2.0'
    compile fileTree(dir: 'libs', include: '*.jar')

}